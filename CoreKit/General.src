// coreKit â€“ a lightweight utility library for GreyScript.
// Built by: _daniel1919 (https://github.com/daniel1919-00)
// 
// ------------------------------------
// -                                  -
// -        GENERAL UTILITIES         -
// -                                  -
// ------------------------------------

coreKit.NEW_LINE = char(10)

coreKit.printOk = function(message)
    print("<color=#7CFC98>" + message)
end function

coreKit.printError = function(message)
    print("<color=#FF7C7C>" + message)
end function

coreKit.printWarning = function(message)
    print("<color=#FFE066>" + message)
end function

coreKit.print = function(message)
    print("<color=#80CFFF>" + message)
end function

// Prints a formatted program header block.
// 
// @param string programName     Name of the program.
// @param string author     	 Author.
// @param string [description]   Optional program description.
coreKit.printHeader = function(programName, author, description = "")
    lines = []
    lines.push("# " + programName)
    lines.push("# Assembled by: " + author)
    lines.push("#")
    
    if description != "" then
        descLines = description.split(coreKit.NEW_LINE)
        for descLine in descLines
            lines.push("# " + descLine.trim)
        end for
    end if

    // Determine the max line length
    maxLen = 0
    for line in lines
        if line.len > maxLen then
            maxLen = line.len
        end if
    end for

    // Subtract 2 chars to account for the initial '# ' in each line
    border = "=" * (maxLen - 2)
    borderLine = "# " + border

    coreKit.print("")
    coreKit.print("")
    coreKit.print(borderLine)
    for line in lines
        coreKit.print(line)
    end for
    coreKit.print(borderLine)
    coreKit.print("")
end function