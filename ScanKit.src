//
// ScanKit â€“ A simple script to quickly scan the network for devices with open ports
// Author: _daniel1919 (https://github.com/daniel1919-00)
//

#import "GrayKit/GrayKit.src"

GrayKit.printHeader("ScanKit", "_daniel1919(https://github.com/daniel1919-00)", "A simple script to quickly scan the network " + GrayKit.NEW_LINE + " for devices with open ports." + GrayKit.NEW_LINE + GrayKit.NEW_LINE + "Note: Pass an ip to target specific machines.")

metax = include_lib("/lib/metaxploit.so")
if metax == null then
    GrayKit.printWarning("Metaxploit library(metaxploit.so) missing, some information will not be available!")
else
    GrayKit.printOk("Metaxploit library loaded, additional information will be available.")
end if

if params.len > 0 then
    targetIp = params[0]
    if is_valid_ip(targetIp) == 0 then
        GrayKit.printError("Invalid ip given ["+targetIp+"]!")
        exit
    end if

    GrayKit.print("Fetching details for target IP ["+targetIp+"]")

    if is_lan_ip(targetIp) then
        router = get_router
    else
        router = get_router(targetIp)
    end if

    if router == null then
        GrayKit.printError("IP address not found [" + targetIp + "]")
        exit
    end if

    devices = [targetIp]
else 
    router = get_router
    devices = router.devices_lan_ip
end if

for ip in devices
    if not is_lan_ip(ip) then
        ports = router.used_ports
    else
        ports = router.device_ports(ip)
    end if

    GrayKit.print("[" + ip + "]")

   if ports != null and ports.len > 0 then
        if metax != null then
            netSession = metax.net_use(ip, ports[0].port_number)
            if netSession != null then
                GrayKit.print("|- User Accounts: " + netSession.get_num_users)
                GrayKit.print("|- Active Users: " + netSession.is_any_active_user)
                GrayKit.print("|- Root User Active: " + netSession.is_root_active_user)
                GrayKit.print("|- Port Forwards: " + netSession.get_num_portforward)
            end if
        end if
   else
       ports = []
   end if

    adminInfo = whois(ip)
    adminInfoLines = adminInfo.split(GrayKit.NEW_LINE)
    GrayKit.print("|- Domain Name: " + adminInfoLines[0].split(":")[1].trim)
    if adminInfoLines.len > 1 then
        GrayKit.print("|- Admin Contact: " + adminInfoLines[1].split(":")[1].trim)
        GrayKit.print("|- Admin Email: " + adminInfoLines[2].split(":")[1].trim)
    end if
    GrayKit.print("|- Open ports")
    openPortsFound = false
    for port in ports
        if port.is_closed then continue

        openPortsFound = true

        GrayKit.print(" |- " +  port.port_number + " -> " + GrayKit.Network.getPortDesc(port.port_number))

        if metax != null then
            netSession = metax.net_use(ip, port.port_number)
            if netSession != null then
                metaLib = netSession.dump_lib
                GrayKit.print("  |- Lib: " + metaLib.lib_name + " v" + metaLib.version)
            end if
        end if
    end for

    if not openPortsFound then
        GrayKit.print(" |- N/A")
    end if
    print(" ")
end for