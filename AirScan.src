//
// AirScan â€“ A simple script to quickly scan the network for devices with open ports
// Built by: _daniel1919 (https://github.com/daniel1919-00)
//

#import "CoreKit/CoreKit.src"

coreKit.printHeader("AirScan", "_daniel1919", "A simple script to quickly scan the network " + coreKit.NEW_LINE + " for devices with open ports." + coreKit.NEW_LINE + coreKit.NEW_LINE + "Note: Pass an ip to target specific machines.")

metax = include_lib("/lib/metaxploit.so")
if metax == null then
    print("Metaxploit library(metaxploit.so) missing, some information will not be available!")
end if

if params.len > 0 then
    targetIp = params[0]
    if is_valid_ip(targetIp) == 0 then
        exit("Invalid ip given ["+targetIp+"]!")
    end if

    print "Fetching details for target IP ["+targetIp+"]"

    if is_lan_ip(targetIp) then
        router = get_router
    else
        router = get_router(targetIp)
    end if

    if router == null then
        exit("IP address not found [" + targetIp + "]")
    end if

    devices = [targetIp]
else 
    router = get_router
    devices = router.devices_lan_ip
end if

for ip in devices
    if not is_lan_ip(ip) then
        ports = router.used_ports
    else
        ports = router.device_ports(ip)
    end if

    print("[" + ip + "]")

   if ports != null and ports.len > 0 then
        if metax != null then
            netSession = metax.net_use(ip, ports[0].port_number)
            if netSession != null then
                print("|- User accounts: " + netSession.get_num_users)
                print("|- User Active: " + netSession.is_any_active_user)
                print("|- Root Active: " + netSession.is_root_active_user)
                print("|- Port forwards: " + netSession.get_num_portforward)
            end if
        end if
   else
       ports = []
   end if

    adminInfo = whois(ip)
    adminInfoLines = adminInfo.split(coreKit.NEW_LINE)
    print("|- Domain name: " + adminInfoLines[0].split(":")[1].trim)
    if adminInfoLines.len > 1 then
        print("|- Admin contact: " + adminInfoLines[1].split(":")[1].trim)
        print("|- Admin email: " + adminInfoLines[2].split(":")[1].trim)
    end if
    print("|- Open ports")
    openPortsFound = false
    for port in ports
        if port.is_closed then continue

        openPortsFound = true

        print(" |- " +  port.port_number + " -> " + coreKit.Network.getPortDesc(port.port_number))

        if metax != null then
            netSession = metax.net_use(ip, port.port_number)
            if netSession != null then
                metaLib = netSession.dump_lib
                print "  |- Lib: " + metaLib.lib_name + " v" + metaLib.version
            end if
        end if
    end for

    if not openPortsFound then
        print " |- N/A"
    end if
    print " "
end for