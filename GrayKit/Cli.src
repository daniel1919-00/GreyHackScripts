// GrayKit â€“ a lightweight utility library for GreyScript.
// Author: _daniel1919 (https://github.com/daniel1919-00)
// 
// ------------------------------------
// -                                  -
// -        CLI UTILITIES             -
// -                                  -
// ------------------------------------

GrayKit.Cli = {}
GrayKit.Cli._flagsParsed = false

// 
// Parses command-line arguments into a map of flags and their values.
// 
// Supports:
//   - Flags with values, e.g. -o /path
//   - Standalone boolean flags, e.g. -t (sets flag to true)
//   - Combined single-character flags, e.g. -ab is equivalent to -a and -b both true
//
GrayKit.Cli.getFlags = function()
	if GrayKit.Cli._flagsParsed then
		return self.flags
	end if

	GrayKit.Cli._flagsParsed = true
	self.flags = {}
	i = 0
    while i < params.len
        param = params[i]
        if param[0] == "-" then
            // Check to see if we have combined flags (e.g. -ab as -a and -b)
            if param.len > 2 then
                for j in range(1, param.len - 1)
                    flag = "-" + param[j]
                    self.flags[flag] = true
                end for
            else
                // Check the next param to see if it is a value(no "-") for the current param
                if i + 1 < params.len and params[i + 1][0] != "-" then
                    self.flags[param] = params[i + 1]
                    i = i + 1
                else
                    self.flags[param] = true  // flag without a value
                end if
            end if
        end if
        i = i + 1
    end while

	return self.flags
end function

// Checks if a specific flag was passed in the command-line arguments.
// 
// @param string flag   The flag name to check (e.g. "o" or "-o").
// @return boolean      True if the flag is present, otherwise false.
GrayKit.Cli.hasFlag = function(flag)
	if flag[0] != "-" then
		flag = "-" + flag	
	end if

	return self.getFlags().hasIndex(flag)
end function

// Retrieves the value associated with a command-line flag.
// 
// @param string flag  The flag name to look up (e.g. "o" or "-o").
// @return boolean     The value of the flag, or null if the flag is not present.
GrayKit.Cli.getFlag = function(flag)
	if flag[0] != "-" then
		flag = "-" + flag	
	end if

	if not self.hasFlag(flag) then
		return null
	end if

	return self.getFlags()[flag]
end function